initialize() {
	if (exists("slimgui")){
		defineConstant("NUM",1);
		defineConstant("PROB_MIG",0.3);
		defineConstant("K", 50);	// carrying capacity
		defineConstant("SEXRATIO",0.5);
		defineConstant("Npop",15);
		defineConstant("m",40);
		defineConstant("NTICKS",20000);
	}

	initializeSLiMModelType("nonWF");

	
	initializeSLiMOptions(nucleotideBased=T);	
	initializeSex();
	defineConstant("LENGTH",1e8);

	defineConstant("FOLDER","/mnt/primevo/work/timothe_dandoy/SLiM_Project/tmp/NWF_FemaleChoice/Migrations/NWFFC_MigV2/");

	
	initializeMutationTypeNuc("m1", 0.5, "f", 0.0);
	m1.convertToSubstitution = T;
//	initializeMutationType("m2", 0.5, "f", 0.2);
	initializeGenomicElementType("g1", m1, 1.0,mmJukesCantor(m*1.25e-8 / 3));
//	initializeGenomicElementType("g2", c(m1,m2), c(10.0,1.0));
	
	initializeChromosome(1,LENGTH,type="A",symbol="A");
	initializeAncestralNucleotides(randomNucleotides(LENGTH));
	initializeGenomicElement(g1, 0, LENGTH-1);
	initializeRecombinationRate(m*1e-8);
	
	initializeChromosome(2,LENGTH,type="X",symbol="X");
	initializeAncestralNucleotides(randomNucleotides(LENGTH));
	initializeGenomicElement(g1, 0, LENGTH-1);
	initializeRecombinationRate(m*1e-8);
	
	initializeChromosome(3,LENGTH,type="Y",symbol="Y");
	initializeAncestralNucleotides(randomNucleotides(LENGTH));
	initializeGenomicElement(g1, 0, LENGTH-1);
	initializeRecombinationRate(0);
}

// each individual reproduces itself once
reproduction() {
	for (pop in sim.subpopulations){
		inds = pop.individuals;
		females = inds[inds.sex == "F"];
		males = inds[inds.sex == "M"];
		//each female reproduce with one male, each male can reproduce multiple times
		for (female in females){
			pop.addCrossed(female, sample(males,1),sex=SEXRATIO);
		}
	}
	self.active = 0;
}

1 early() {
	for (i in 0:(Npop-1)){
		sim.addSubpop(i,K,SEXRATIO);
	}	
	deleteFile(FOLDER+"NWFFC_subpop_star_Mig"+PROB_MIG+","+NUM+".txt");
}

early() {
	// Males migration
	inds = sim.subpopulations.individuals;
	males = inds[(inds.sex == "M")];
	nMigrants = min(rpois(1,size(males)*PROB_MIG),size(males));
	migrants = sample(males,nMigrants);
	for (migrant in migrants){
		do dest = sample(sim.subpopulations, 1);
		while (dest == migrant.subpopulation);
		dest.takeMigrants(migrant);
	}
	
	sim.subpopulations.fitnessScaling = K / sim.subpopulations.individualCount;
}

NTICKS late() {

	hapA = sim.subpopulations.haplosomesForChromosomes("A", includeNulls=F);
	hapX = sim.subpopulations.haplosomesForChromosomes("X", includeNulls=F);
   hapY = sim.subpopulations.haplosomesForChromosomes("Y", includeNulls=F);

	PiA  = calcPi(hapA);
	PiX  = calcPi(hapX);
	PiY  = calcPi(hapY);
		
	writeFile(FOLDER+"NWFFC_subpop_star_Mig"+PROB_MIG+","+NUM+".txt", paste(c(PiA,PiX,PiY)),append=T);


	for (pop in sim.subpopulations){
		hapA = pop.haplosomesForChromosomes("A", includeNulls=F);
		hapX = pop.haplosomesForChromosomes("X", includeNulls=F);
   	hapY = pop.haplosomesForChromosomes("Y", includeNulls=F);

		PiA  = calcPi(hapA);
		PiX  = calcPi(hapX);
		PiY  = calcPi(hapY);
		
		writeFile(FOLDER+"NWFC_subpop_star_Mig"+PROB_MIG+","+NUM+".txt", paste(c(PiA,PiX,PiY)),append=T);
	}
}
	