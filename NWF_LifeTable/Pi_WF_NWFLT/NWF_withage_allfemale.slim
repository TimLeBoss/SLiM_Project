initialize() {
	if (exists("slimgui")){
		defineConstant("NUM",1);
		defineConstant("SEXRATIO",0.5);
	}

	initializeSLiMModelType("nonWF");
	defineConstant("K", 500);	// carrying capacity
	
	initializeSLiMOptions(nucleotideBased=T);	
	initializeSex();
	defineConstant("LENGTH",1e8);
	defineConstant("FOLDER","/mnt/primevo/work/timothe_dandoy/SLiM_Project/tmp/NWFwithageallfemale/");
	// Life tables provided by Fernando
	defineConstant("L", c(0.215909091, 0.092391304, 0.045908184, 0.054393305, 0.037610619, 0.03908046, 0.04784689, 0.037688442, 0.041775457, 0.046321526, 0.034285714, 0.065088757, 0.060126582, 0.087542088, 0.081180812, 0.088353414, 0.083700441, 0.125, 0.093406593, 0.163636364, 0.115942029, 0.147540984, 0.240384615, 0.164556962, 0.333333333, 0.363636364, 0.428571429, 1));

	// Bronikowski et al. 
	defineConstant("fertility_F", c(0.000, 0.000, 0.000, 0.000, 0.005, 0.211, 0.264, 0.272, 0.279, 0.281, 0.269, 0.276, 0.290, 0.291, 0.285, 0.274, 0.263, 0.271, 0.271, 0.236, 0.233, 0.259, 0.047, 0.108, 0.000, 0.000, 0.000, 0.000));
	
	initializeMutationTypeNuc("m1", 0.5, "f", 0.0);
	m1.convertToSubstitution = T;
//	initializeMutationType("m2", 0.5, "f", 0.2);
	initializeGenomicElementType("g1", m1, 1.0,mmJukesCantor(1.25e-8 / 3));
//	initializeGenomicElementType("g2", c(m1,m2), c(10.0,1.0));
	
	initializeChromosome(1,LENGTH,type="A",symbol="A");
	initializeAncestralNucleotides(randomNucleotides(LENGTH));
	initializeGenomicElement(g1, 0, LENGTH-1);
	initializeRecombinationRate(1e-8);
	
	initializeChromosome(2,LENGTH,type="X",symbol="X");
	initializeAncestralNucleotides(randomNucleotides(LENGTH));
	initializeGenomicElement(g1, 0, LENGTH-1);
	initializeRecombinationRate(1e-8);
}

//each female reproduce with one male, each male can reproduce with multiple females
reproduction(NULL,"F") {
	if (sample(c(T, F), 1, weights = c(fertility_F[individual.age], 1-fertility_F[individual.age]))){
		mate = subpop.sampleIndividuals(1, sex ="M", minAge=5);
		if (size(mate) != 0){subpop.addCrossed(individual, mate,sex=SEXRATIO);}
	}
}

1 early() {
	sim.addSubpop("p1",500,SEXRATIO);
	p1.individuals.age = rdunif(1,0,27);	
}

early() {
	// Change individual mortality based on sex-dependent life table
	inds = p1.individuals;
	ages = inds.age;
	sexes = inds.sex;
	mortality = L[ages];
	survival = 1 - mortality;
	inds.fitnessScaling = survival;
	// density-dependence, factoring in individual mortality
	if (mean(survival)!= 0.0){p1.fitnessScaling = K / (p1.individualCount * mean(survival));}	
}

10000 late() {

	hapA = sim.subpopulations.haplosomesForChromosomes("A", includeNulls=F);
	hapX = sim.subpopulations.haplosomesForChromosomes("X", includeNulls=F);

	
	PiA  = calcHeterozygosity(hapA);
	PiX  = calcHeterozygosity(hapX);

	

	writeFile(FOLDER+"Allfemale_SEXRATIO:"+SEXRATIO+","+NUM+".txt", paste(c(PiA,PiX)));
}
