initialize() {
	if (exists("slimgui")){
		defineConstant("NUM",1);
		defineConstant("NTICKS",10000);
	}
	
	defineConstant("SEXRATIO",0.5);
	initializeSLiMModelType("nonWF");
	defineConstant("K", 50);	// carrying capacity
	
	initializeSLiMOptions(nucleotideBased=T);
	initializeSex();
	defineConstant("LENGTH",1e8);
	
	defineConstant("FOLDER","/mnt/primevo/work/timothe_dandoy/SLiM_Project/tmp/Test_Pop10x50_NWFV/");
	
	
	initializeMutationTypeNuc("m1", 0.5, "f", 0.0);
	m1.convertToSubstitution = T;
	//	initializeMutationType("m2", 0.5, "f", 0.2);
	initializeGenomicElementType("g1", m1, 1.0,mmJukesCantor(1.25e-8 / 3));
	//	initializeGenomicElementType("g2", c(m1,m2), c(10.0,1.0));
	
	initializeChromosome(1,LENGTH,type="A",symbol="A");
	initializeAncestralNucleotides(randomNucleotides(LENGTH));
	initializeGenomicElement(g1, 0, LENGTH-1);
	initializeRecombinationRate(1e-8);
	
	initializeChromosome(2,LENGTH,type="X",symbol="X");
	initializeAncestralNucleotides(randomNucleotides(LENGTH));
	initializeGenomicElement(g1, 0, LENGTH-1);
	initializeRecombinationRate(1e-8);
	
	initializeChromosome(3,LENGTH,type="Y",symbol="Y");
	initializeAncestralNucleotides(randomNucleotides(LENGTH));
	initializeGenomicElement(g1, 0, LENGTH-1);
	initializeRecombinationRate(0);
}

// each individual reproduces itself once
reproduction() {
	for (pop in sim.subpopulations){
		inds = pop.individuals;
		females = inds[inds.sex == "F"];
		males = inds[inds.sex == "M"];
		females = sample(females,size(females));
		males = sample(males,size(males));
		//each female reproduce with one male, each male can reproduce multiple times
		Nrepro = min(size(females),size(males));
		for (repro in 0:(Nrepro-1)){
			pop.addCrossed(females[repro], males[repro],sex=SEXRATIO);
		}
	}
	self.active = 0;
}

1 early() {
	for (i in 1:10){
		sim.addSubpop(i,50,SEXRATIO);
	}
}

early() {
	sim.subpopulations.fitnessScaling = K / sim.subpopulations.individualCount;
}

NTICKS late() {
	hapA = sim.subpopulations.haplosomesForChromosomes("A", includeNulls=F);
	hapX = sim.subpopulations.haplosomesForChromosomes("X", includeNulls=F);
	hapY = sim.subpopulations.haplosomesForChromosomes("Y", includeNulls=F);
	
	PiA  = calcHeterozygosity(hapA);
	PiX  = calcHeterozygosity(hapX);
	PiY  = calcHeterozygosity(hapY);
	
	WaA = calcWattersonsTheta(hapA);
	WaX = calcWattersonsTheta(hapX);
	WaY = calcWattersonsTheta(hapY);

	piA = calcPi(hapA);
	piX = calcPi(hapX);	
	piY = calcPi(hapY);
	
	writeFile(FOLDER+"NWFV_Pop10x50_NTicks"+NTICKS+","+NUM+".txt",paste(c(PiA,PiX,PiY,WaA,WaX,WaY,piA,piX,piY)));
}
